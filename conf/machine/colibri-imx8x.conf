#@TYPE: Machine
#@NAME: Colibri iMX8X
#@DESCRIPTION: Colibri iMX8X powered by a i.MX 8X SoC
#@MAINTAINER: Max Krummenacher <max.krummenacherr@toradex.com>

MACHINE_NAME = "Colibri-iMX8X"

MACHINEOVERRIDES =. "mx8:mx8x:mx8qxp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = " \
    freescale/fsl-imx8qxp-colibri-eval-v3.dtb \
    freescale/fsl-imx8qxp-colibri-dsihdmi-eval-v3.dtb \
    freescale/fsl-imx8qxp-colibri-lvds-dual-eval-v3.dtb \
    freescale/fsl-imx8qxp-colibri-lvds-single-eval-v3.dtb \
"

UBOOT_MAKE_TARGET = "u-boot.bin"
UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "colibri-imx8qxp_config,sdcard"

BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

MACHINE_BOOT_FILES = "boot.scr"
IMAGE_BOOT_FILES_append = " ${MACHINE_BOOT_FILES}"
WKS_FILE_DEPENDS_append = " u-boot-distro-boot"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"
# Currently we use the nxp 2018.03 downstream for mx8 as some features are
# not yet available in 2019.03.
PREFERRED_VERSION_u-boot-toradex = "2018.03"
PREFERRED_VERSION_u-boot-toradex-fw-utils = "2018.03"

MACHINE_FIRMWARE_append = " linux-firmware-pcie8997"
MACHINE_FIRMWARE_append = " firmware-imx-vpu-imx8"

IMXBOOT_TARGETS = "flash"

PREFERRED_PROVIDER_virtual/libg2d_imxdpu = "imx-dpu-g2d"
PREFERRED_VERSION_wayland-protocols_mx8 = "1.16.imx"
PREFERRED_VERSION_weston_mx8 = "5.0.0.imx"

BOARD_TYPE = "colibri"

IMAGE_CLASSES_append = " image_type_tezi"
IMAGE_FSTYPES += "teziimg"

# The imx-boot container takes care for the i.MX 8 offset, so the container
# has to be flashed at offset 0 directly
UBOOT_BINARY_TEZI_EMMC="imx-boot"
OFFSET_BOOTROM_PAYLOAD="0"
do_image_teziimg[depends] += "imx-boot:do_deploy"
do_image_teziimg_distro[depends] += "imx-boot:do_deploy"

TORADEX_PRODUCT_IDS = "0038 0050 0051 0052"
TORADEX_PRODUCT_IDS[0038] = "fsl-imx8qxp-colibri-eval-v3.dtb"
TORADEX_PRODUCT_IDS[0050] = "fsl-imx8qxp-colibri-eval-v3.dtb"
TORADEX_PRODUCT_IDS[0051] = "fsl-imx8qxp-colibri-eval-v3.dtb"
TORADEX_PRODUCT_IDS[0052] = "fsl-imx8qxp-colibri-eval-v3.dtb"
