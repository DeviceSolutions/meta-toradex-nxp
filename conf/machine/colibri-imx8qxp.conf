#@TYPE: Machine
#@NAME: Colibri iMX8QXP
#@DESCRIPTION: Colibri iMX8QXP powered by a i.MX 8QXP SoC
#@MAINTAINER: Max Krummenacher <max.krummenacherr@toradex.com>

MACHINEOVERRIDES =. "mx8:mx8x:mx8qxp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyAMA1"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = " \
    freescale/fsl-imx8qxp-colibri-eval-v3.dtb \
    freescale/fsl-imx8qxp-colibri-dsihdmi-eval-v3.dtb \
    freescale/fsl-imx8qxp-colibri-lvds-dual-eval-v3.dtb \
    freescale/fsl-imx8qxp-colibri-lvds-single-eval-v3.dtb \
"

UBOOT_MAKE_TARGET = "u-boot.bin"
UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "colibri-imx8qxp_config,sdcard"

BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex"

MACHINE_FIRMWARE_append = " linux-firmware-pcie8997"
MACHINE_FIRMWARE_append = " firmware-imx-vpu-imx8"

IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_flexspi', \
                                                                'flash_all flash', d)}"

BOARD_TYPE = "colibri"

