#@TYPE: Machine
#@NAME: Apalis iMX8
#@DESCRIPTION: Apalis iMX8 powered by a i.MX 8QuadMax SoC
#@MAINTAINER: Stefan Agner <stefan.agner@toradex.com>

# until we get it from meta-freescale
# https://github.com/Freescale/meta-freescale/commit/3dc8f2fe59866395681e1aa0565b1d98ca27043e
HOSTTOOLS_NONFATAL_append = " sha384sum"

MACHINE_NAME = "Apalis-iMX8"

SOC_SILICON_REV ?= ""

MACHINEOVERRIDES =. "mx8:mx8qm:${@bb.utils.contains('SOC_SILICON_REV', 'a0', 'mx8qma0:', '', d)}"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/fsl-imx8qm-apalis.dtb"

UBOOT_MAKE_TARGET = "u-boot.bin"
UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "apalis-imx8_config,sdcard"

BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"
IMX_BOOT_SEEK_mx8qma0 = "33"

EXTRA_IMAGEDEPENDS += "firmware-imx-8"
IMAGE_BOOT_FILES += "${MACHINE_BOOT_FILES}"
MACHINE_BOOT_FILES = "hdmitxfw.bin dpfw.bin"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex"

MACHINE_FIRMWARE_append = " linux-firmware-pcie8997"
MACHINE_FIRMWARE_append = " firmware-imx-vpu-imx8"

IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_b0_flexspi', \
                                                                'flash_b0', d)}"

IMXBOOT_TARGETS_mx8qma0 = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_flexspi', \
                       bb.utils.contains('UBOOT_CONFIG', 'nand', 'flash_nand', \
                                                                 'flash_dcd', d), d)}"

PREFERRED_PROVIDER_virtual/libg2d_imxdpu = "imx-dpu-g2d"
PREFERRED_VERSION_wayland-protocols_mx8 = "1.16.imx"
PREFERRED_VERSION_weston_mx8 = "5.0.0.imx"

BOARD_TYPE = "apalis"
BOARD_TYPE_mx8qma0 = "a0-apalis"

IMAGE_CLASSES_append = " image_type_tezi"
IMAGE_FSTYPES += "teziimg"

# The imx-boot container takes care for the i.MX 8 offset, so the container
# has to be flashed at offset 0 directly
UBOOT_BINARY_TEZI_EMMC="imx-boot"
OFFSET_BOOTROM_PAYLOAD="0"
do_image_teziimg[depends] += "imx-boot:do_deploy"
do_image_teziimg_distro[depends] += "imx-boot:do_deploy"

TORADEX_PRODUCT_IDS = "0037"
TORADEX_PRODUCT_IDS[0037] = "fsl-imx8qm-apalis.dtb"
