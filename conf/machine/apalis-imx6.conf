#@TYPE: Machine
#@NAME: Toradex Apalis iMX6Q/D
#@SOC: i.MX6
#@DESCRIPTION: Machine configuration for Toradex Apalis iMX6 SOM
#@MAINTAINER: Max Krummenacher <max.krummenacher@toradex.com>

MACHINEOVERRIDES =. "mx6:mx6q:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

PREFERRED_PROVIDER_virtual/kernel ?= "linux-toradex"
KERNEL_DEVICETREE += "imx6q-apalis-eval.dtb imx6q-apalis_v1_0-eval.dtb \
                     imx6q-apalis-ixora.dtb imx6q-apalis_v1_0-ixora.dtb \
                     imx6q-apalis-ixora-v1.1.dtb"
KERNEL_IMAGETYPE = "uImage"
# The kernel lives in a seperate FAT partition, don't deploy it in /boot/
RDEPENDS_kernel-base = ""

PREFERRED_PROVIDER_u-boot ?= "u-boot-toradex"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-toradex"
SPL_BINARY = "SPL"
# The SPL configures the DDR RAM depending on the module it runs on. Thus there
# is no need to distingush between the different module types.
# For recovery mode we build a U-Boot with save timings for all known Apalis iMX6
# Note: The last config will be symlinked to UBOOT_BINARY which is crucial for
# certain image formats.
UBOOT_CONFIG ??= "recover spl"
UBOOT_CONFIG[spl] = "apalis_imx6_defconfig,,u-boot.img"
UBOOT_CONFIG[recover] = "apalis_imx6_nospl_it_defconfig,,u-boot.imx"
UBOOT_MAKE_TARGET = ""
# The payload offsets are given in number of 512 byte blocks.
OFFSET_BOOTROM_PAYLOAD = "2"
OFFSET_SPL_PAYLOAD = "138"

IMAGE_CLASSES += "image_type_tezi"
IMAGE_FSTYPES = "tar.xz teziimg"

SERIAL_CONSOLE ?= "115200 ttymxc0"
TORADEX_PRODUCT_IDS_apalis-imx6 = "0027 0028 0029 0035"

MACHINE_FEATURES += "screen usbgadget usbhost vfat ext2 alsa touchscreen wifi bluetooth 3g pci"
